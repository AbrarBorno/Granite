#version 450
#extension GL_EXT_samplerless_texture_functions : require
layout(local_size_x = 8, local_size_y = 8) in;

#define SUBGROUP_OPS
#include "../inc/subgroup_extensions.h"
#include "../inc/project_direction.h"

layout(set = 0, binding = 0) uniform texture2D uDepth;
layout(set = 0, binding = 1) uniform texture2D uNormal;
layout(set = 0, binding = 2) uniform texture2D uPBR;
layout(set = 0, binding = 3) uniform texture2DArray uNoise;
layout(set = 0, binding = 4) writeonly uniform image2D uOutputUV;

layout(set = 1, binding = 0) uniform UBO
{
    mat4 view_projection;
    mat4 inv_view_projection;
    vec2 float_resolution;
    vec2 inv_resolution;
    uvec2 resolution;
    int max_lod;
    int frame;
    vec3 camera_position;
};

float FFX_SSSR_LoadDepth(ivec2 coord, int lod)
{
    return texelFetch(uDepth, coord, lod).x;
}

vec3 FFX_SSSR_LoadWorldSpaceNormal(ivec2 coord)
{
    return texelFetch(uNormal, coord, 0).xyz * 2.0 - 1.0;
}

vec3 FFX_SSSR_ScreenSpaceToWorldSpace(vec3 ndc)
{
    vec4 world_clip = inv_view_projection * vec4(ndc, 1.0);
    return world_clip.xyz / world_clip.w;
}

// From FidelityFX-SSSR.
/**********************************************************************
Copyright (c) 2021 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/

const float M_PI = 3.1415628;

// http://jcgt.org/published/0007/04/01/paper.pdf by Eric Heitz
// Input Ve: view direction
// Input alpha_x, alpha_y: roughness parameters
// Input U1, U2: uniform random numbers
// Output Ne: normal sampled with PDF D_Ve(Ne) = G1(Ve) * max(0, dot(Ve, Ne)) * D(Ne) / Ve.z
vec3 SampleGGXVNDF(vec3 Ve, float alpha_x, float alpha_y, float U1, float U2)
{
    // Section 3.2: transforming the view direction to the hemisphere configuration
    vec3 Vh = normalize(vec3(alpha_x * Ve.x, alpha_y * Ve.y, Ve.z));
    // Section 4.1: orthonormal basis (with special case if cross product is zero)
    float lensq = Vh.x * Vh.x + Vh.y * Vh.y;
    vec3 T1 = lensq > 0 ? vec3(-Vh.y, Vh.x, 0) * inversesqrt(lensq) : vec3(1, 0, 0);
    vec3 T2 = cross(Vh, T1);
    // Section 4.2: parameterization of the projected area
    float r = sqrt(U1);
    float phi = 2.0 * M_PI * U2;
    float t1 = r * cos(phi);
    float t2 = r * sin(phi);
    float s = 0.5 * (1.0 + Vh.z);
    t2 = (1.0 - s) * sqrt(1.0 - t1 * t1) + s * t2;
    // Section 4.3: reprojection onto hemisphere
    vec3 Nh = t1 * T1 + t2 * T2 + sqrt(max(0.0, 1.0 - t1 * t1 - t2 * t2)) * Vh;
    // Section 3.4: transforming the normal back to the ellipsoid configuration
    vec3 Ne = normalize(vec3(alpha_x * Nh.x, alpha_y * Nh.y, max(0.0, Nh.z)));
    return Ne;
}

vec3 Sample_GGX_VNDF_Ellipsoid(vec3 Ve, float alpha_x, float alpha_y, float U1, float U2)
{
    return SampleGGXVNDF(Ve, alpha_x, alpha_y, U1, U2);
}

vec3 Sample_GGX_VNDF_Hemisphere(vec3 Ve, float alpha, float U1, float U2)
{
    return Sample_GGX_VNDF_Ellipsoid(Ve, alpha, alpha, U1, U2);
}

mat3 CreateTBN(vec3 N)
{
    vec3 U;

    if (abs(N.z) > 0.0)
    {
        float k = sqrt(N.y * N.y + N.z * N.z);
        U.x = 0.0; U.y = -N.z / k; U.z = N.y / k;
    }
    else
    {
        float k = sqrt(N.x * N.x + N.y * N.y);
        U.x = N.y / k; U.y = -N.x / k; U.z = 0.0;
    }

    return mat3(U, cross(N, U), N);
}

vec2 SampleRandomVector2D(ivec2 pixel)
{
    return texelFetch(uNoise, ivec3(pixel.xy & 127, frame), 0).xy;
}

vec3 SampleReflectionVector(vec3 view_direction, vec3 normal, float roughness, ivec2 dispatch_thread_id)
{
    mat3 tbn_transform = CreateTBN(normal);
    vec3 view_direction_tbn = -view_direction * tbn_transform;

    vec2 u = SampleRandomVector2D(dispatch_thread_id);

    vec3 sampled_normal_tbn = Sample_GGX_VNDF_Hemisphere(view_direction_tbn, roughness, u.x, u.y);

    vec3 reflected_direction_tbn = reflect(-view_direction_tbn, sampled_normal_tbn);

    // Transform reflected_direction back to the initial space.
    return tbn_transform * reflected_direction_tbn;
}

bool IsMirrorReflection(float roughness)
{
    return roughness < 0.0001;
}

const float FFX_SSSR_FLOAT_MAX = 3.402823466e+38;

void FFX_SSSR_InitialAdvanceRay(vec3 origin, vec3 direction, vec3 inv_direction,
        vec2 current_mip_resolution, vec2 current_mip_resolution_inv,
        vec2 floor_offset, vec2 uv_offset, out vec3 position, out float current_t)
{
    vec2 current_mip_position = current_mip_resolution * origin.xy;

    // Intersect ray with the half box that is pointing away from the ray origin.
    vec2 xy_plane = floor(current_mip_position) + floor_offset;
    xy_plane = xy_plane * current_mip_resolution_inv + uv_offset;

    // o + d * t = p' => t = (p' - o) / d
    vec2 t = xy_plane * inv_direction.xy - origin.xy * inv_direction.xy;
    current_t = min(t.x, t.y);
    position = origin + current_t * direction;
}

bool FFX_SSSR_AdvanceRay(vec3 origin, vec3 direction, vec3 inv_direction,
        vec2 current_mip_position, vec2 current_mip_resolution_inv,
        vec2 floor_offset, vec2 uv_offset, float surface_z,
        inout vec3 position, inout float current_t)
{
    // Create boundary planes
    vec2 xy_plane = floor(current_mip_position) + floor_offset;
    xy_plane = xy_plane * current_mip_resolution_inv + uv_offset;
    vec3 boundary_planes = vec3(xy_plane, surface_z);

    // Intersect ray with the half box that is pointing away from the ray origin.
    // o + d * t = p' => t = (p' - o) / d
    vec3 t = boundary_planes * inv_direction - origin * inv_direction;

    // Prevent using z plane when shooting out of the depth buffer.
    t.z = direction.z > 0 ? t.z : FFX_SSSR_FLOAT_MAX;

    // Choose nearest intersection with a boundary.
    float t_min = min(min(t.x, t.y), t.z);

    // Smaller z means closer to the camera.
    bool above_surface = surface_z > position.z;

    // Decide whether we are able to advance the ray until we hit the xy boundaries or if we had to clamp it at the surface.
    // We use the asuint comparison to avoid NaN / Inf logic,
    // also we actually care about bitwise equality here to see if t_min is the t.z we fed into the min3 above.
    bool skipped_tile = floatBitsToUint(t_min) != floatBitsToUint(t.z) && above_surface;

    // Make sure to only advance the ray if we're still above the surface.
    current_t = above_surface ? t_min : current_t;

    // Advance ray
    position = origin + current_t * direction;

    return skipped_tile;
}

vec2 FFX_SSSR_GetMipResolution(vec2 screen_dimensions, int mip_level)
{
    return screen_dimensions * ldexp(1.0, -mip_level);
}

// Requires origin and direction of the ray to be in screen space [0, 1] x [0, 1]
vec3 FFX_SSSR_HierarchicalRaymarch(vec3 origin, vec3 direction,
        bool is_mirror,
        int most_detailed_mip, uint min_traversal_occupancy,
        uint max_traversal_intersections, out bool valid_hit)
{
    vec3 inv_direction = mix(vec3(FFX_SSSR_FLOAT_MAX), 1.0 / direction, notEqual(direction, vec3(0.0)));

    // Start on mip with highest detail.
    int current_mip = most_detailed_mip;

    // Could recompute these every iteration, but it's faster to hoist them out and update them.
    vec2 current_mip_resolution = FFX_SSSR_GetMipResolution(float_resolution, current_mip);
    vec2 current_mip_resolution_inv = 1.0 / current_mip_resolution;

    // Offset to the bounding boxes uv space to intersect the ray with the center of the next pixel.
    // This means we ever so slightly over shoot into the next region.
    vec2 uv_offset = 0.005 * exp2(most_detailed_mip) * inv_resolution;
    uv_offset = mix(uv_offset, -uv_offset, lessThan(direction.xy, vec2(0.0)));

    // Offset applied depending on current mip resolution to move the boundary to the left/right upper/lower border depending on ray direction.
    vec2 floor_offset = mix(vec2(1.0), vec2(0.0), lessThan(direction.xy, vec2(0.0)));

    // Initially advance ray to avoid immediate self intersections.
    float current_t;
    vec3 position;

    FFX_SSSR_InitialAdvanceRay(origin, direction, inv_direction,
            current_mip_resolution, current_mip_resolution_inv,
            floor_offset, uv_offset, position, current_t);

    bool exit_due_to_low_occupancy = false;
    int i = 0;
    while (i < max_traversal_intersections && current_mip >= most_detailed_mip && !exit_due_to_low_occupancy)
    {
        vec2 current_mip_position = current_mip_resolution * position.xy;
        float surface_z = FFX_SSSR_LoadDepth(ivec2(current_mip_position), current_mip);
        uint active_lanes = subgroupBallotBitCount(subgroupBallot(true));
        exit_due_to_low_occupancy = !is_mirror && active_lanes <= min_traversal_occupancy;
        bool skipped_tile = FFX_SSSR_AdvanceRay(origin, direction, inv_direction,
                current_mip_position, current_mip_resolution_inv,
                floor_offset, uv_offset, surface_z, position, current_t);
        current_mip += skipped_tile ? 1 : -1;
        current_mip_resolution *= skipped_tile ? 0.5 : 2;
        current_mip_resolution_inv *= skipped_tile ? 2 : 0.5;
        ++i;
    }

    valid_hit = i <= max_traversal_intersections;

    return position;
}

float FFX_SSSR_ValidateHit(vec3 hit, vec2 uv,
        vec3 world_space_ray_direction,
        float depth_buffer_thickness)
{
    // Reject hits outside the view frustum
    if (any(lessThan(vec4(hit.xy, 1.0, 1.0), vec4(0.0, 0.0, hit.xy))))
        return 0.0;

    // Reject the hit if we didnt advance the ray significantly to avoid immediate self reflection
    vec2 manhattan_dist = abs(hit.xy - uv);
    if (all(lessThan(manhattan_dist, 2.0 * inv_resolution)))
        return 0.0;

    // Lookup radiance from the far plane.
    // maister: Why is this sampled from LOD 1 and not 0? :|
    ivec2 texel_coords = ivec2(float_resolution * hit.xy);
    float surface_z = FFX_SSSR_LoadDepth(texel_coords / 2, 1);
    if (surface_z == 1.0)
        return 1.0;

    // We check if we hit the surface from the back, these should be rejected.
    vec3 hit_normal = FFX_SSSR_LoadWorldSpaceNormal(texel_coords);
    if (dot(hit_normal, world_space_ray_direction) > 0.0)
        return 0.0;

    // maister: Was view space, but we already have view_projection matrices.
    // This is fine, albeit slightly less accurate.
    // This can be optimized since we only change Z here. So we can convert to linear depth delta
    // with adjustment based on XY position.
    vec3 view_space_surface = FFX_SSSR_ScreenSpaceToWorldSpace(vec3(hit.xy, surface_z));
    vec3 view_space_hit = FFX_SSSR_ScreenSpaceToWorldSpace(hit);
    float distance = length(view_space_surface - view_space_hit);

    // Fade out hits near the screen borders
    vec2 fov = 0.05 * vec2(float_resolution.y * inv_resolution.x, 1.0);
    vec2 border = smoothstep(vec2(0.0), fov, hit.xy) * (1.0 - smoothstep(1.0 - fov, vec2(1.0), hit.xy));
    float vignette = border.x * border.y;

    // We accept all hits that are within a reasonable minimum distance below the surface.
    // Add constant in linear space to avoid growing of the reflections toward the reflected objects.
    float confidence = 1.0 - smoothstep(0.0, depth_buffer_thickness, distance);
    confidence *= confidence;

    return vignette * confidence;
}
////

vec3 trace_inner()
{
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = (vec2(coord) + 0.5) * inv_resolution;
    vec2 clip_uv = 2.0 * uv - 1.0;

    float clip_depth = texelFetch(uDepth, ivec2(coord), 0).x;
    if (clip_depth == 1.0)
        return vec3(-1.0, -1.0, 0.0);

    vec3 world_pos = FFX_SSSR_ScreenSpaceToWorldSpace(vec3(clip_uv, clip_depth));
    vec3 d = normalize(world_pos - camera_position);
    vec3 N = FFX_SSSR_LoadWorldSpaceNormal(coord);
    float roughness = texelFetch(uPBR, coord, 0).g;
    vec3 reflected = SampleReflectionVector(d, N, roughness, ivec2(gl_GlobalInvocationID.xy));
    vec3 screen_space_dir = project_direction_to_clip_space(vec3(clip_uv, clip_depth), reflected, view_projection);

    vec3 ndc = vec3(uv, clip_depth);
    screen_space_dir.xy *= 0.5;

    bool is_mirror = IsMirrorReflection(roughness);
    const int most_detailed_mip = 0;
    const uint min_traversal_occupancy = 4;
    const uint max_traversal_intersections = 64;
    bool valid_hit;

    vec3 result = FFX_SSSR_HierarchicalRaymarch(ndc, screen_space_dir, is_mirror,
            most_detailed_mip,
            min_traversal_occupancy, max_traversal_intersections, valid_hit);

    if (!valid_hit)
        return vec3(-1.0, -1.0, 0.0);

    const float depth_buffer_thickness = 0.20;

    float confidence = FFX_SSSR_ValidateHit(result, uv, reflected, depth_buffer_thickness);
    return vec3(result.xy, confidence);
}

void main()
{
    if (all(lessThan(gl_GlobalInvocationID.xy, resolution)))
    {
        vec3 uv_confidence = trace_inner();
        // Shift to [-0.5, 0.5] range for more FP16 precision.
        uv_confidence.xy -= 0.5;
        imageStore(uOutputUV, ivec2(gl_GlobalInvocationID.xy), vec4(uv_confidence, 0.0));
    }
}